buildscript {
    ext.kotlin_version = '1.4.21-2'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'io.codearte.nexus-staging' version '0.22.0' //can only be applied to root project
}

apply from: 'versionTasks.gradle'

nexusStaging {
    username = "$System.env.SONATYPE_NEXUS_USERNAME"
    password = "$System.env.SONATYPE_NEXUS_PASSWORD"
}

allprojects {
    group 'com.github.tschuchortdev'

    buildscript {
        repositories {
            mavenCentral()
            jcenter()
            maven { url 'https://jitpack.io' }
            maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
            google()
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://kotlin.bintray.com/kotlinx' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        google()
        jcenter()
    }

    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }

        if (JavaVersion.current().isJava8Compatible()) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        //noinspection GroovyAccessibility // alternatively replace this with archiveClassifier.set('...')
        archiveClassifier = 'sources'
        from sourceSets.main.allSource, 'build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        //noinspection GroovyAccessibility //alternatively replace this with archiveClassifier.set('...')
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    afterEvaluate {
        archivesBaseName = POM_ARTIFACT_ID
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    groupId = GROUP
                    artifactId = POM_ARTIFACT_ID
                    version = VERSION_NAME

                    name = POM_NAME
                    packaging = POM_PACKAGING
                    description = POM_DESCRIPTION
                    url = POM_URL

                    from components.java
                    artifact sourcesJar
                    artifact javadocJar

                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                credentials {
                    username "$System.env.SONATYPE_NEXUS_USERNAME"
                    password "$System.env.SONATYPE_NEXUS_PASSWORD"
                }
                url = VERSION_NAME.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY : RELEASE_REPOSITORY
            }
        }
    }

    signing {
        // Skip signing if publishing to mavenLocal and credentials are not available
        required { !gradle.taskGraph.hasTask("publishToMavenLocal") && project.hasProperty("signing.keyId") }

        if (!project.hasProperty("signing.secretKeyRingFile") && !project.hasProperty("signing.password")) {
            /* If in CI, the signing password and signing key are defined in the environment variables
             ORG_GRADLE_PROJECT_signingPassword and ORG_GRADLE_PROJECT_signingKey
             (https://blog.solidsoft.pl/2020/06/03/simpler-and-safer-artifact-signing-on-ci-server-with-gradle/,
             https://stackoverflow.com/questions/57921325/gradle-signarchives-unable-to-read-secret-key).
             Otherwise they are defined in the user's home directory gradle.properties.
             */
            useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
        }

        sign publishing.publications.mavenJava
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

        testImplementation group: 'junit', name: 'junit', version: '4.13.1'
        testImplementation "org.mockito:mockito-core:3.7.0"
        testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
        testImplementation "org.assertj:assertj-core:3.18.1"
    }

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}
