buildscript {
    ext.kotlin_version = '1.3.72'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'

    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.vanniktech.maven.publish'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    jcenter()
}

idea {
    module {
        sourceDirs += files('build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main')
        generatedSourceDirs += files('build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main')
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation group: 'junit', name: 'junit', version: '4.12'

    compileOnly "com.google.auto.service:auto-service:1.0-rc2"
    kapt "com.google.auto.service:auto-service:1.0-rc2"

    testImplementation "org.assertj:assertj-core:3.11.1"
    testImplementation "org.mockito:mockito-core:3.2.4"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"

    testImplementation 'com.squareup:kotlinpoet:1.4.0'
    testImplementation 'com.squareup:javapoet:1.11.1'

    implementation "com.squareup.okio:okio:2.1.0"
    implementation 'io.github.classgraph:classgraph:4.8.86'

    // This dependency is only needed as a "sample" compiler plugin to test that
    // running compiler plugins passed via the pluginClasspath CLI option works
    testRuntime "org.jetbrains.kotlin:kotlin-scripting-compiler:$kotlin_version"

    // The Kotlin compiler should be near the end of the list because its .jar file includes
    // an obsolete version of Guava
    implementation "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-annotation-processing-embeddable:$kotlin_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
}

test {
    systemProperty "KOTLIN_VERSION", "$kotlin_version"
}
