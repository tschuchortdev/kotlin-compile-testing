//file:noinspection GroovyAssignabilityCheck
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType

plugins {
    id "com.github.gmazzo.buildconfig" version "5.3.5"
}

apply plugin: 'kotlin-kapt'

buildConfig {
    className = "BuildConfig"
    packageName = "com.tschuchort.compiletesting"
    sourceSets {
        test {
            buildConfigField 'String', 'KOTLIN_VERSION', "\"$embedded_kotlin_version\""
        }
    }
}

/* Multiple variants are offered of the dependencies kotlin-dom-api-compat and kotlin-stdlib-js. Usually, Gradle picks
* them automatically based on what kind of build it is, i.e. it would look for a platform JVM variant for this JVM build.
* Naturally, there is no JVM version for JS libraries. We need to fix the variant attributes manually so the right variant
* will be picked for runtime use in the JS compile tests. */
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("org.jetbrains.kotlin:kotlin-dom-api-compat"))
                .using variant(module("org.jetbrains.kotlin:kotlin-dom-api-compat:$embedded_kotlin_version")) {
            attributes {
                attribute(Attribute.of("org.jetbrains.kotlin.platform.type", KotlinPlatformType.class), KotlinPlatformType.js)
                attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, "kotlin-runtime"))
            }
        }
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute(module("org.jetbrains.kotlin:kotlin-stdlib-js"))
                .using variant(module("org.jetbrains.kotlin:kotlin-stdlib-js:$embedded_kotlin_version")) {
            attributes {
                attribute(Attribute.of("org.jetbrains.kotlin.platform.type", KotlinPlatformType.class), KotlinPlatformType.js)
                attribute(Usage.USAGE_ATTRIBUTE, project.objects.named(Usage, "kotlin-runtime"))
                attribute(Attribute.of("org.jetbrains.kotlin.js.compiler", String), "ir")
            }
        }
    }
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service:1.1.1'
    kapt "com.google.auto.service:auto-service:1.1.1"


    testImplementation 'com.squareup:kotlinpoet:1.15.2'
    testImplementation 'com.squareup:javapoet:1.13.0'

    implementation 'com.squareup.okio:okio:3.9.0'
    implementation 'io.github.classgraph:classgraph:4.8.171'

    // This dependency is only needed as a "sample" compiler plugin to test that
    // running compiler plugins passed via the pluginClasspath CLI option works
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-scripting-compiler:$embedded_kotlin_version"

    // Include Kotlin/JS standard library in test classpath for auto loading
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-stdlib-js"
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-dom-api-compat"

    // The Kotlin compiler should be near the end of the list because its .jar file includes
    // an obsolete version of Guava
    api "org.jetbrains.kotlin:kotlin-compiler-embeddable:$embedded_kotlin_version"
    api "org.jetbrains.kotlin:kotlin-annotation-processing-embeddable:$embedded_kotlin_version"
}

compileKotlin {
    // https://github.com/tschuchortdev/kotlin-compile-testing/pull/63
    kotlinOptions.freeCompilerArgs += ["-Xno-optimized-callable-references"]
    kotlinOptions.freeCompilerArgs += ["-opt-in=org.jetbrains.kotlin.compiler.plugin.ExperimentalCompilerApi"]
}

compileTestKotlin {
    // https://github.com/tschuchortdev/kotlin-compile-testing/pull/63
    kotlinOptions.freeCompilerArgs += ["-Xno-optimized-callable-references"]
    kotlinOptions.freeCompilerArgs += ["-opt-in=org.jetbrains.kotlin.compiler.plugin.ExperimentalCompilerApi"]
}
