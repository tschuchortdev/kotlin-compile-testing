
plugins {
    id "com.github.gmazzo.buildconfig" version "3.0.3"
}

apply plugin: 'kotlin-kapt'

buildConfig {
    className = "BuildConfig"
    packageName = "com.tschuchort.compiletesting"
    sourceSets {
        test {
            buildConfigField 'String', 'KOTLIN_VERSION', "\"$embedded_kotlin_version\""
        }
    }
}

dependencies {
    compileOnly "com.google.auto.service:auto-service:1.0.1"
    kapt "com.google.auto.service:auto-service:1.0.1"


    testImplementation 'com.squareup:kotlinpoet:1.12.0'
    testImplementation 'com.squareup:javapoet:1.13.0'

    implementation "com.squareup.okio:okio:3.1.0"
    implementation 'io.github.classgraph:classgraph:4.8.147'

    // This dependency is only needed as a "sample" compiler plugin to test that
    // running compiler plugins passed via the pluginClasspath CLI option works
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-scripting-compiler:$embedded_kotlin_version"

    // Include Kotlin/JS standard library in test classpath for auto loading
    testRuntimeOnly "org.jetbrains.kotlin:kotlin-stdlib-js"

    // The Kotlin compiler should be near the end of the list because its .jar file includes
    // an obsolete version of Guava
    api "org.jetbrains.kotlin:kotlin-compiler-embeddable:$embedded_kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-annotation-processing-embeddable:$embedded_kotlin_version"
}

compileKotlin {
    // https://github.com/tschuchortdev/kotlin-compile-testing/pull/63
    kotlinOptions.freeCompilerArgs += ["-Xno-optimized-callable-references"]
    kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
}

compileTestKotlin {
    // https://github.com/tschuchortdev/kotlin-compile-testing/pull/63
    kotlinOptions.freeCompilerArgs += ["-Xno-optimized-callable-references"]
    kotlinOptions.freeCompilerArgs += ["-Xskip-runtime-version-check"]
}
